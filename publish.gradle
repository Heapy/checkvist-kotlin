apply(plugin: "maven-publish")
apply(plugin: "java-library")
apply(plugin: "com.jfrog.bintray")
apply(plugin: "org.jetbrains.dokka")

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

dokka {
    outputFormat = "html"
    outputDirectory = javadoc.destinationDir
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    from javadoc.destinationDir
    classifier = "javadoc"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId project.group
            artifactId project.name
            version project.version

            artifact sourcesJar
            artifact dokkaJar

            pom {
                name = project.name
                description = project.description
                url = "https://heapy.io/"

                licenses {
                    license {
                        name = "GNU General Public License v3.0"
                        url = "https://www.gnu.org/licenses/gpl-3.0-standalone.html"
                    }
                }

                developers {
                    developer {
                        id = "IRus"
                        name = "Ruslan Ibragimov"
                        email = "ruslan@ibragimov.by"
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/Heapy/checkvist-kotlin.git"
                    developerConnection = "scm:git:git@github.com:Heapy/checkvist-kotlin.git"
                    url = "https://github.com/Heapy/checkvist-kotlin"
                }
            }

            from components.java
        }
    }
}

boolean isDev = project.version.contains("dev")

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")

    pkg {
        userOrg = "heapy"
        repo = isDev ? "heap-dev" : "heap"
        name = "checkvist-kotlin"
        websiteUrl = "https://heapy.io/"
        publish = isDev
        licenses = ["GPL-3.0"]
        vcsUrl = "https://github.com/Heapy/checkvist-kotlin"
        publications = ["maven"]
        publicDownloadNumbers = true
        version {
            name = project.version
            released = new Date()
            vcsTag = project.version
        }
    }
}
